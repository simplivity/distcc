#!/usr/bin/perl

use strict;
use warnings FATAL => "all";

my $distcc_dir = "/usr/lib/distcc";
my $old_gcc_dir = "/usr/lib/gcc";
my $new_gcc_dir = "/usr/lib/%DEB_HOST_MULTIARCH%/gcc";
my %old_symlinks; # Current compiler names in /usr/lib/distcc
my %new_symlinks; # Compiler names that should be in /usr/lib/distcc
my @standard_names = qw(cc c++);

sub consider {
    my ($name) = @_;
    if (-x "/usr/bin/$name") {
        $new_symlinks{$name} = 1;
    }
}

sub consider_gcc {
    my ($prefix, $suffix) = @_;
    consider "${prefix}gcc${suffix}";
    consider "${prefix}g++${suffix}";
}

# Find existing standard compiler names.
foreach (@standard_names) {
    consider $_;
}

# Find existing GCC variants.
consider_gcc "", "";
consider_gcc "c89-", "";
consider_gcc "c99-", "";
foreach my $dir (<$old_gcc_dir/*>, <$new_gcc_dir/*>) {
    (my $kind = $dir) =~ s|.*/||;
    consider_gcc "$kind-", "";
    foreach (<$dir/*>) {
        if (! -l $_ and -d $_) {
            s|.*/||;
            consider_gcc "", "-$_";
            consider_gcc "$kind-", "-$_";
        }
    }
}

# Find existing symlinks.
foreach (<$distcc_dir/*>) {
    if (-l) {
        s|.*/||;
        $old_symlinks{$_} = 1;
    }
}

# Remove obsolete symlinks.
foreach (keys %old_symlinks) {
    if (! exists $new_symlinks{$_}) {
        unlink "$distcc_dir/$_";
    }
}

# Add missing symlinks.
foreach (keys %new_symlinks) {
    if (! exists $old_symlinks{$_}) {
        symlink "../../bin/distcc", "$distcc_dir/$_";
    }
}
